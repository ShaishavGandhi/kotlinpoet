buildscript {
  ext.versions = [
      'kotlin': '1.3.21',
      'dokka': '0.9.17',
      'truth': '0.42',
      'compileTesting': '0.15',
      'jimfs': '1.1',
      'ecj': '4.6.1',
      'mavenPublish': '0.8.0-SNAPSHOT',
  ]
  ext.deps = [
      'kotlin': [
          'gradle': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdlib': [
              'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7",
          ],
          'reflect': "org.jetbrains.kotlin:kotlin-reflect",
          'test': [
              'junit': "org.jetbrains.kotlin:kotlin-test-junit"
          ]
      ],
      'truth': "com.google.truth:truth:${versions.truth}",
      'compileTesting': "com.google.testing.compile:compile-testing:${versions.compileTesting}",
      'jimfs': "com.google.jimfs:jimfs:${versions.jimfs}",
      'ecj': "org.eclipse.jdt.core.compiler:ecj:${versions.ecj}",
      'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
      'mavenPublish': "com.vanniktech:gradle-maven-publish-plugin:${versions.mavenPublish}",
  ]
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath deps.kotlin.gradle
    classpath deps.dokka
    classpath deps.mavenPublish
  }
}

group = GROUP
version = VERSION_NAME

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.vanniktech.maven.publish'

repositories {
  mavenCentral()
}

jar {
  manifest {
    attributes('Automatic-Module-Name': 'com.squareup.kotlinpoet')
  }
}

afterEvaluate {
  dokka {
    skipDeprecated = true
    outputFormat = 'html'
  }
}

dependencies {
  api deps.kotlin.stdlib.jdk7
  implementation deps.kotlin.reflect
  testImplementation deps.kotlin.test.junit
  testImplementation deps.truth
  testImplementation deps.compileTesting
  testImplementation deps.jimfs
  testImplementation deps.ecj
}
